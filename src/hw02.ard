\import Utils
\import hw01 (fac)

-- 1. Определите полиморфный tail для списков.

\func tail {A : \Type} (xs : List A) : List A \elim xs
  | nil => nil
  | :: x xs => xs

-- 2. Реализуйте любой (but choose wisely) алгоритм сортировки. Функцию сравнения можно принимать в качестве аргумента.

-- Comp returns whether the first argument is not greater then the second one
\func sort {A : \Type} (xs : List A) (comp : A -> A -> Bool) : List A \elim xs
  | nil => nil
  | :: x xs => insert (sort xs comp) x comp
    \where {
      \func insert {A : \Type} (xs : List A) (x : A) (comp : A -> A -> Bool) : List A \elim xs
        | nil => x :: nil
        | :: x' xs => if (comp x x') (x :: x' :: xs) (x' :: insert xs x comp)
    }

-- 3. Докажите ассоциативность && и ||.

\func &&-assoc (x y z : Bool) : (x && y) && z = x && (y && z) \elim x
  | true => idp
  | false => idp

\func ||-assoc (x y z : Bool) : (x || y) || z = x || (y || z) \elim x
  | true => idp
  | false => idp

-- 4. Докажите, что fac 3 равно 6.

\func fac-3-eq-6 : fac 3 = 6 => idp

-- 5. Докажите следующие пропозициональные тавтологии (choose 2 out of 3).

-- t1 : (P and Q -> R) -> P -> Q -> R
\func t1 {P Q R : \Type} (f : \Sigma P Q -> R) (p : P) (q : Q) : R => f (p, q)

-- t2 : (P -> R) -> (Q -> R) -> P or Q -> R
\func t2 {P Q R : \Type} (f : P -> R) (g : Q -> R) (h : Either P Q) : R \elim h
  | inl a => f a
  | inr b => g b

-- t3 : ((P -> Q -> R) -> P) -> (P -> R) -> R
\func t3 {P Q R : \Type} (f : (P -> Q -> R) -> P) (g : P -> R) : R => g (f (\lam p _ => g p))

-- 6. Сформулируйте и докажите следующие пропозициональные тавтологии (choose 2 out of 3).

-- t4 : (P -> Q -> R) -> P and Q -> R

\func t4 {P Q R : \Type} (f : P -> Q -> R) (x : \Sigma P Q) : R => \case x \with {
  | (a, b) => f a b
}

-- t5 : (P or Q -> P and Q) -> (P -> Q) and (Q -> P)

\func t5 {P R Q : \Type} (f : Either P Q -> \Sigma P Q) : \Sigma (P -> Q) (Q -> P) => (
  \lam p => (f (inl p)).2,
  \lam q => (f (inr q)).1
)

-- t6 : ((((P -> Q) -> P) -> P) -> Q) -> Q

\func t6 {P Q R : \Type} (f : (((P -> Q) -> P) -> P) -> Q) : Q =>
  f (\lam p1 => p1 (\lam p2 => f (\lam _ => p2)))

-- 7. Докажите следующие утверждения.

-- s1 : Если для любого x : Nat верно P(x), то существует x : Nat для которого P(x) верно.
\func s1 (P : Nat -> \Type) (h : \Pi (x : Nat) -> P x) : \Sigma (x : Nat) (P x) => (0, h 0)

-- s2 : Если не существует x : Nat такого, что верно P(x), то P(3) не верно.
\func s2 (P : Nat -> \Type) (h : \Sigma (x : Nat) (P x) -> Empty) : P 3 -> Empty => \lam x => h (3, x)

-- 8. Сформулируйте и докажите следующие утверждения.

-- s3 : Если для всех x : Nat верно, что P(x) влечет Q(x), и существует x : Nat для которого верно P(x), то существует x : Nat для которого верно Q(x).

\func s3 (P Q : Nat -> \Type) (h : \Pi (x : Nat) -> P x -> Q x) (e : \Sigma (x : Nat) (P x)) : \Sigma (x : Nat) (Q x) \elim e
  | (x, ex) => (x, h x ex)

-- s4 : Если для всех x : Nat либо не верно P(x), либо не верно Q(x), то P(3) влечет, что Q(3) не верно.

\func s4 (P Q : Nat -> \Type) (h : \Pi (x : Nat) -> Either (P x -> Empty) (Q x -> Empty)) (t : P 3) : Q 3 -> Empty =>
  \case h 3 \with {
    | inl a => \lam q => a t
    | inr b => b
  }
